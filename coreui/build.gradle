apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            buildConfigField "boolean", "DOGFOOD", 'false'
            buildConfigField "boolean", "USE_CRASHLYTICS", 'true'
        }
        debug {
            buildConfigField "boolean", "DOGFOOD", 'false'
            buildConfigField "boolean", "USE_CRASHLYTICS", 'false'
        }
        dogfood {
            buildConfigField "boolean", "DOGFOOD", 'true'
            buildConfigField "boolean", "USE_CRASHLYTICS", 'true'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // Always show the result of every unit test, even if it passes.
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
            reports.junitXml.destination = file('../build/test-results/')
        }
    }
}

kapt {
    useBuildCache = true
}

allOpen {
    annotation("piuk.blockchain.androidcore.utils.annotations.Mockable")
}

androidExtensions {
    experimental = true
}

dependencies {
    api project(':core')
    // Support Libs
    api Libraries.appCompat
    api Libraries.recyclerView
    api Libraries.cardView
    api Libraries.gridLayout
    api Libraries.design
    api Libraries.v13
    api Libraries.v14
    api Libraries.dynamicAnims
    api Libraries.constraintLayout
    // Dagger
    kapt Libraries.daggerKapt
    // Rx UI Bindings
    api Libraries.rxBinding
    api Libraries.rxFingerprint
    // Logging
    api(Libraries.crashlytics) { transitive = true }

    // Unit Test dependencies
    testImplementation Libraries.junit
    testImplementation Libraries.mockito
    testImplementation Libraries.hamcrestJunit
    testImplementation Libraries.kotlinJunit
    testImplementation Libraries.kluent
    testImplementation(Libraries.mockitoKotlin) {
        exclude module: 'kotlin-stdlib'
    }
}
